//xlang 

class FileInputStream : Stream{
	long filehandler;

	public FileInputStream(String path){
		filehandler = XPlatform.openSystemFile(path, "r");
        if (filehandler == 0){
            throw new IllegalArgumentException("file " + path + " can not open");
        }
	}

	public bool attach(String path){
		if (filehandler != 0){
			_system_.closeFile(filehandler);
			filehandler = 0;
		}
		filehandler = XPlatform.openSystemFile(path, "r");
        return filehandler != 0;
	}
    public void checkValid(){
        if (filehandler == 0){
            throw new IllegalArgumentException("The Stream is not ready");
        }
    }
	public long available(bool bwait){
		return length() - getPosition();
    }
	public long seek(int type, long pos){
		checkValid();
		return _system_.seekFile(filehandler, type, pos);
    }
    
    public long getPosition(){
		checkValid();
		return _system_.getFileSeek(filehandler);
    }
    
	public long length(){
		checkValid();
		return _system_.fileLength(filehandler);
	}

	public byte[] read(){
		checkValid();
		byte[] data = new byte[length()];
		_system_.readFile(filehandler, data, 0, data.length);
		return data;
	}

	public int read(byte [] data , int position, int length)override{
		checkValid();
		return _system_.readFile(filehandler, data, position, length);
	}

	public int write(byte [] data , int position, int length)override{
		return 0;
    }
    
    public void close(){
		if (filehandler != 0){
			_system_.closeFile(filehandler);
			filehandler = 0;
		}
    }
    
	public void finalize(){
		close();
	}
};