//xlang Source, Name:TextDetail.xcsm 
//Date: Sun Sep 01:45:35 2018 

class TextDetail : QXDialog {

	String title = "详细信息";
	String text;
    QXSci _sci;
    static QXRect rect = nilptr;
    public interface closeListener{
		void onCreate();
		void onClose(String text, bool updated);
    };
    
    closeListener cl_lis ;
    
	public TextDetail(closeListener l){
		cl_lis = l;
    }
    
	public TextDetail(){
    
    }

    public void syntaxForOutput(){
		if (Setting.isDarkStyle()){
			syntaxForOutputDark();
            return ;
		}
        _sci.sendEditor(QXSci.SCI_SETCODEPAGE, QXSci.SC_CP_UTF8);
        _sci.setWrap(true);
        _sci.sendEditor(QXSci.SCI_STYLESETBACK, QXSci.STYLE_DEFAULT, 0xffffffff);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, QXSci.STYLE_DEFAULT, 0xff222827);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, 75, 0xff222827);
        _sci.sendEditor(QXSci.SCI_STYLECLEARALL, 0, 0);
        //_sci.sendEditor(QXSci.STYLE_LINENUMBER, 1, 0);
        _sci.sendEditor(QXSci.SCI_STYLESETFONT, QXSci.STYLE_DEFAULT,Setting.getEditorFont()); 
        _sci.sendEditor(QXSci.SCI_STYLESETSIZEFRACTIONAL, QXSci.STYLE_DEFAULT,Setting.getEditorFontSize()); 
        _sci.sendEditor(QXSci.SCI_STYLECLEARALL, 0, 0); 
        _sci.sendEditor(QXSci.SCI_SETEOLMODE, 1, 0); 
        
        _sci.sendEditor(QXSci.SCI_SETMARGINTYPEN, 0, QXSci.SC_MARGIN_NUMBER); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 0, 40); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 1, 10); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 2, 0); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 3, 0); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 4, 0); 

        _sci.sendEditor(QXSci.SCI_STYLESETBACK, QXSci.STYLE_LINENUMBER, 0xffefefef);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, QXSci.STYLE_LINENUMBER, 0xffaf912b);
        _sci.sendEditor(QXSci.SCI_SETMARGINLEFT, 0, 0);
        
        _sci.sendEditor(QXSci.SCI_SETCARETFORE,0xff000000,0);
        
        _sci.sendEditor(QXSci.SCI_SETCARETLINEVISIBLE, 1); 
        _sci.sendEditor(QXSci.SCI_SETCARETLINEBACK, 0xffefefef); 

        _sci.sendEditor(QXSci.SCI_SETTABWIDTH, 4); 
    }
    
    public void syntaxForOutputDark(){
        _sci.sendEditor(QXSci.SCI_SETCODEPAGE, QXSci.SC_CP_UTF8);
        _sci.sendEditor(QXSci.SCI_STYLESETBACK, QXSci.STYLE_DEFAULT, 0xff262525);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, QXSci.STYLE_DEFAULT, 0xffefefef);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, 75, 0xffefefef);
        _sci.sendEditor(QXSci.SCI_STYLECLEARALL, 0, 0);
        //_sci.sendEditor(QXSci.STYLE_LINENUMBER, 1, 0);
        _sci.sendEditor(QXSci.SCI_STYLESETFONT, QXSci.STYLE_DEFAULT,Setting.getEditorFont()); 
        _sci.sendEditor(QXSci.SCI_STYLESETSIZEFRACTIONAL, QXSci.STYLE_DEFAULT,Setting.getEditorFontSize()); 
        _sci.sendEditor(QXSci.SCI_STYLECLEARALL, 0, 0); 
        //_sci.sendEditor(QXSci.SCI_SETREADONLY, 1);
        //C++语法解析 
        //_sci.sendEditor(QXSci.SCI_SETLEXER, QXSci.SCLEX_CPP, 0); 
        //_sci.sendEditor(QXSci.SCI_SETKEYWORDS, 0, szKeywords1);//设置关键字 
        //_sci.sendEditor(QXSci.SCI_SETKEYWORDS, 1, szKeywords2);//设置关键字 
        // 下面设置各种语法元素风格 
        _sci.sendEditor(QXSci.SCI_SETEOLMODE, 1, 0); 
        
        _sci.sendEditor(QXSci.SCI_SETMARGINTYPEN, 0, QXSci.SC_MARGIN_NUMBER); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 0, 40); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 1, 10); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 2, 0); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 3, 0); 
        _sci.sendEditor(QXSci.SCI_SETMARGINWIDTHN, 4, 0); 
        
        _sci.sendEditor(QXSci.SCI_STYLESETBACK, QXSci.STYLE_LINENUMBER, 0xff262525);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, QXSci.STYLE_LINENUMBER, 0xff666666);
        _sci.sendEditor(QXSci.SCI_SETMARGINLEFT, 0, 0);
        
        _sci.sendEditor(QXSci.SCI_SETCARETFORE,0xffffffff,0);
        
        _sci.sendEditor(QXSci.SCI_SETCARETLINEVISIBLE, 1); 
        _sci.sendEditor(QXSci.SCI_SETCARETLINEBACK, 0xff202020); 
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, QXSci.SCE_C_COMMENT, 0xff666666);
        _sci.sendEditor(QXSci.SCI_STYLESETFORE, QXSci.SCE_C_COMMENTLINE, 0xff666666);
        
    }
    
    public bool onClose(){
        rect = new QXRect(x(), y(), width(), height());
		if (cl_lis != nilptr){
			String update_txt = _sci.getText();
			cl_lis.onClose(update_txt, text.equals(update_txt) == false);
        }
        return true;
    }
    
   public bool create(String caption,@NotNilptr String str, QXWidget parent, bool modal){
		title = caption;
		text = str;
        if (parent == nilptr){
            parent = XWorkspace.workspace;
        }
		if (super.create(XWorkspace.workspace)){
			_sci = new QXSci();
			if (_sci.create(this)){
				syntaxForOutput();
                _sci.sendEditor(QXSci.SCI_SETEOLMODE, QXSci.SC_EOL_LF, 0);
				setOnLayoutEventListener(new onLayoutEventListener(){
						void onResize(QXObject obj, int w, int h, int oldw, int oldh)override {
							if (_sci != nilptr){
								_sci.resize(w, h );
							}
						}
				});
			
				//setWindowFlags(CustomizeWindowHint | WindowMinMaxButtonsHint | WindowCloseButtonHint);
                setWindowFlags(CustomizeWindowHint | WindowCloseButtonHint  | Dialog  | WindowTitleHint);
				setWindowTitle(title);
				_sci.setText(str);
                

				if (modal){
                    setModal(true);
                }
				resize(400, 300);
				
                
                if (cl_lis == nilptr){
					_sci.setReadOnly(true);
                }else{
					cl_lis.onCreate();
                }
                
                if (rect != nilptr){
                    move(rect.left,rect.top);
                    resize(rect.right,rect.bottom);
                }
                
                createContextMenu();
                    
                show();
                
                setOnActivateListener(new onActivateListener(){
					void onWindowActivate(@NotNilptr QXObject obj){
						((QXWidget)obj).setOpacity(1.0);
					}
					void onWindowDeactivate(QXObject obj){
						((QXWidget)obj).setOpacity(0.6);
					}
				});
				return true;
            }
        }
        return false;
    }
    
    
    public static class ContextMenu{
		public QXMenu contextMenu = new QXMenu();
		public QXAction [] actions;
		
		public void create(@NotNilptr QXWidget parent,@NotNilptr  String [] acts, onEventListener listener){
			if (contextMenu.create(parent)){
				actions = new QXAction[acts.length];
				for (int i = 0; i < acts.length; i ++){
					QXAction action = new QXAction();
					if (action.create(contextMenu)){
						
						if (acts[i].equals("-")){
							action.setSeparator(true);
						}else{
							action.setEnable(true);	
							action.setText(acts[i]);
							action.setOnEventListener(listener);
						}
					}
					actions[i] = action;
				}
				parent.setContextMenuPolicy(ActionsContextMenu);
				parent.addActions(actions);
			}
		}
	};
    
    
    ContextMenu _contextMenu = new ContextMenu();
    
    public void createContextMenu(){
        static String _default_folder = "", _default_openpath = "", _default_savepath = "";
		onEventListener menuListener = new onEventListener(){
           String toRelativePath(String ref, String path, bool cov){
            bool breslash = false;
               if ((_system_.getPlatformId() == _system_.PLATFORM_WINDOWS) && Setting.isUnixPath()){
                   breslash = true;
               }
               path = String.formatPath(path, breslash);
               if (cov == false || ref == nilptr){
                   return path;
               }
               return path.toRelativePath(ref,false,true);
               
           }
		   void onTrigger(QXObject obj) override{
				if (obj == _contextMenu.actions[0]){
                    String path = QXFileDialog.getFolderPath("浏览文件夹","",_default_folder, TextDetail.this);
                    if (path != nilptr && path.length() > 0){
                        bool breal = false;
                        String pjdir = XWorkspace.workspace.currentProjectDir();
                        if (pjdir != nilptr && QXMessageBox.Question("提示","是否转换为当前项目的相对路径?",QXMessageBox.Yes | QXMessageBox.No,QXMessageBox.Yes) == QXMessageBox.Yes){
                            breal = true;
                        }
                        _default_folder = path;
                        _sci.insertText(_sci.currentPosition(),toRelativePath(pjdir, path, breal));
                    }
                }else
                if (obj == _contextMenu.actions[2]){
                    String [] path = QXFileDialog.getOpenFileNames("浏览文件", _default_openpath, "*",TextDetail.this);
                    if (path != nilptr){
                        bool breal = false;
                        String pjdir = XWorkspace.workspace.currentProjectDir();
                        if (pjdir != nilptr && QXMessageBox.Question("提示","是否转换为当前项目的相对路径?",QXMessageBox.Yes | QXMessageBox.No,QXMessageBox.Yes) == QXMessageBox.Yes){
                            breal = true;
                        }
                        
                        for (int i =0; i < path.length; i++){
                            if (i == 0){
                                _default_openpath = path[i].findVolumePath();
                            }
                            _sci.insertText(_sci.currentPosition(),toRelativePath(pjdir, path[i], breal) + "\n");
                        }
                    }
                }else
                if (obj == _contextMenu.actions[3]){
                    String path = QXFileDialog.getSaveFileName("选择保存位置",_default_savepath,"*",TextDetail.this);
                    if (path != nilptr && path.length() > 0){
                        bool breal = false;
                        String pjdir = XWorkspace.workspace.currentProjectDir();
                        if (pjdir != nilptr && QXMessageBox.Question("提示","是否转换为当前项目的相对路径?",QXMessageBox.Yes | QXMessageBox.No,QXMessageBox.Yes) == QXMessageBox.Yes){
                            breal = true;
                        }
                        
                        _default_savepath = path;
                        _sci.insertText(_sci.currentPosition(),toRelativePath(pjdir, path, breal));
                    }
                }else
                if (obj == _contextMenu.actions[5]){
                    _sci.Copy();
                }else
                if (obj == _contextMenu.actions[6]){
                    _sci.Paste();
                }else
                if (obj == _contextMenu.actions[8]){
                    TextDetail.this.close();
                }
		   }
		};
        
		
		
		String []acts = {"浏览文件夹", "-", "浏览文件", "选择保存位置",  "-", "复制", "粘贴", "-", "关闭"};
		_contextMenu.create(_sci, acts, menuListener);
	}
};